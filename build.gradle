buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.2'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildToolsVersion "21.1.0"
    compileSdkVersion 21

    defaultConfig {
        applicationId "org.qii.weiciyuan"
        minSdkVersion 17
        targetSdkVersion 19
        versionCode - 1
        versionName null
    }

    sourceSets {
        main {
            manifest {
                srcFile 'AndroidManifest.xml'
            }
            java {
                srcDir 'src'
            }
            res {
                srcDir 'res'
            }
            assets {
                srcDir 'assets'
            }
            resources {
                srcDir 'src'
            }
            jniLibs {
                srcDir 'libs'
            }
            aidl {
                srcDir 'src/com/sina/sso'
            }

        }

        debug {
            res {
                srcDir 'debug/res'
            }
        }

    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            if (System.getenv("KEYSTORE") != null) {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    /*
     * android:targetPackage cant use ${applicationId}, so define a string source called "applicationId"
     */

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "applicationId", "org.qii.weiciyuan" + getApplicationIdSuffix()
            debuggable true
            runProguard false
            signingConfig signingConfigs.debug
        }
        release {
            resValue "string", "applicationId", "org.qii.weiciyuan" + getApplicationIdSuffix()
            debuggable false
            runProguard false
            signingConfig signingConfigs.release
        }

    }


    dexOptions {
        preDexLibraries = false
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:21.0.0'
    compile(project(':libs:slidingmenulibrary')) {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile(project(':libs:showcaseviewlibrary')) {
        exclude group: 'com.google.android', module: 'support-v4'

    }
}


